OBJECT: 
 STRING APIKEY,
 STRING URL,
 STRING CONTENTTYPEXMLJSON,
 STRING XMLTEXT,
 DATE STARTDATE,
 DATE ENDDATE,
 STRING STARTDATETEXT,
 STRING ENDDATETEXT,
 STRING CURRENCY,
 TABLE TMPTABLE,
 TABLE CURRTABLE,
 STRING CURRNAME,
 STRING STRPARSE;

APIKEY = '#API KEY#'; //evds2.tcmb.gov.tr SİTESİNDEN ALMIŞ OLDUĞUNUZ APIKEY'İ BURAYA GİRİNİZ

STARTDATE = SUBDAYS(SYS_CURRENTDATE,1);
ENDDATE = SYS_CURRENTDATE;
STARTDATETEXT = REPLACE(STARTDATE,'.','-');
ENDDATETEXT = REPLACE(ENDDATE,'.','-');
CURRENCY = '';
STRPARSE = '';

IF STRINGVAR1 == '' THEN
	CURRENCY = 'TP.DK.USD.A-TP.DK.EUR.A-TP.DK.CHF.A-TP.DK.GBP.A-TP.DK.JPY.A-TP.DK.USD.S-TP.DK.EUR.S-TP.DK.CHF.S-TP.DK.GBP.S-TP.DK.JPY.S';
ENDIF;


IF STRINGVAR1 == 'ALIŞ' || STRINGVAR1 == 'PURCHASE' THEN
	CURRENCY = 'TP.DK.USD.A-TP.DK.EUR.A-TP.DK.CHF.A-TP.DK.GBP.A-TP.DK.JPY.A';
ENDIF;


IF STRINGVAR1 == 'SATIŞ' || STRINGVAR1 == 'SALES' THEN
	CURRENCY = 'TP.DK.USD.S-TP.DK.EUR.S-TP.DK.CHF.S-TP.DK.GBP.S-TP.DK.JPY.S';
ENDIF;


IF STRINGVAR1 == 'EUR' THEN
	CURRENCY = 'TP.DK.EUR.A-TP.DK.EUR.S';
ENDIF;


IF STRINGVAR1 == 'USD' THEN
	CURRENCY = 'TP.DK.USD.A-TP.DK.USD.S';
ENDIF;

URL = 'https://evds2.tcmb.gov.tr/service/evds/series='+CURRENCY+'&startDate=' + STARTDATETEXT + '&endDate=' + ENDDATETEXT + '&type=xml';
CONTENTTYPEXMLJSON =  'Content-Type|,application/xml|;Accept|,*/*|;key|,'+APIKEY;
SENDHTTPPOST '' TO URL HEADERS CONTENTTYPEXMLJSON CODEPAGE 'UTF-8' REQUESTMETHOD GET;

IF SYS_STATUS THEN
	MESSAGE EFE E0 WITH 'GEÇERSİZ HTTP İSTEĞİ !';
	STRINGVAR3=URL;
	RETURN;
ENDIF;

XMLTEXT = SYS_HTTPPOSTRESPONSE;
STRINGVAR3= WRAPXML(XMLTEXT);

CLEAR XMLMAP CURRMAP;
CREATEXMLMAP CURRMAP;

MAP ELEMENT document AS XMLROOTTABLE document IN CURRMAP;
MAP ELEMENT items AS XMLTABLE CURRTABLE IN CURRMAP;
MAP CHILD Tarih OF items AS XMLCOLUMN Tarih IN CURRMAP;

PARSE CURRENCY INTO STRPARSE DELIMITER '-' 
BEGIN
	CURRNAME = REPLACE(STRPARSE,'.','_');
	MAP CHILD @CURRNAME OF items AS XMLCOLUMN @CURRNAME IN CURRMAP;
ENDPARSE;

MAP RELATION items TO document LINK primaryKey WITH foreignKey GENERATE YES IN CURRMAP;
PARSEXML TEXT XMLTEXT USING CURRMAP;
CONVERTXMLTABLE CURRTABLE INTO TABLE CURRTABLE FROM CURRMAP;
COPY TABLE CURRTABLE INTO TMPTABLE;
SET TMPTABLE TO TABLE TMPTABLE;
SETCOL TMPTABLE_foreignKey TO HIDE;
